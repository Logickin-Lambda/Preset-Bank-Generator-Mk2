// *********************************************************************************************
// Construct the whole setting page by components
// 
// Input: 
// Output: 
//   - GUI container that represents the whole setting page, containing
// 	   - all the interactive components
// 	   - a look up table for accessing component simipler, without seeking the array index
// 	   - additional controller state for customized behaviors 
//
// ********************************************************************************************* 
fn construct_preset_converter_options_gui () {

    $options_gui = GUI_new( -1, //parent
                    -0, -0, //x, y position
                    0, 0, //width, height
                    BLACK, 0, //color, transparency
                    -1 //texture
                    )

	$options_gui.gui_ptr = 0
    
    $options_gui.children[$options_gui.gui_ptr] = gui_window_frame_new($options_gui, 0, 0, 800, 600, "Preset Generator", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.app_border = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    // Build the main complex components 
	build_input_path_input($options_gui)
	build_output_folder_name($options_gui)
	// build_output_path_input($options_gui)
	build_export_as_check_box($options_gui)
	build_optimization_radio_button($options_gui)
	build_message_box($options_gui)

    // Export button
    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, 275, 240, "Export", COLOR_INFO)
	$options_gui.gui_comp_lkup.export_btn = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

	// define control state
	$options_gui.export_as_radio_btn_state = 1


    ret($options_gui)
}

fn build_input_path_input($options_gui){

    // Input Path:
    $options_gui.children[$options_gui.gui_ptr] = text_message_new($options_gui, -290, -230, "Input Path:", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.input_path_label = $options_gui.children[$options_gui.gui_ptr].str[0].text
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, -280, -245, "", COLOR_SECONDARY)
    $options_gui.children[$options_gui.gui_ptr].w = 550
    $options_gui.children[$options_gui.gui_ptr].children[0].w = 544
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].x - 40
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].flags = 0b00000100
	$options_gui.gui_comp_lkup.input_path_text_box = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, 275, -245, "Load", COLOR_INFO)
	$options_gui.gui_comp_lkup.input_path_load_btn = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1
}

fn build_output_folder_name($options_gui){
    // File Name:
    $options_gui.children[$options_gui.gui_ptr] = text_message_new($options_gui, -290, -180, "File Name:", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.file_name_label = $options_gui.children[$options_gui.gui_ptr].str[0].text
	$options_gui.gui_ptr + 1


    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, -280, -195, "", COLOR_SECONDARY)
    $options_gui.children[$options_gui.gui_ptr].w = 550
    $options_gui.children[$options_gui.gui_ptr].children[0].w = 544
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].x - 40
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].flags = 0b00000100
	$options_gui.gui_comp_lkup.file_name_text_box = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1
}

fn build_output_path_input($options_gui){
	// Output Path:
    $options_gui.children[$options_gui.gui_ptr] = text_message_new($options_gui, -290, -130, "Output Path:", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.output_path_label = $options_gui.children[$options_gui.gui_ptr].str[0].text
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, -280, -145, "", COLOR_SECONDARY)
    $options_gui.children[$options_gui.gui_ptr].w = 550
    $options_gui.children[$options_gui.gui_ptr].children[0].w = 544
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].x - 40
    $options_gui.children[$options_gui.gui_ptr].children[0].str[ 0 ].flags = 0b00000100
	$options_gui.gui_comp_lkup.output_path_text_box = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = button_new($options_gui, 275, -145, "Load", COLOR_INFO)
	$options_gui.gui_comp_lkup.output_path_load_btn = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1
}

fn build_export_as_check_box($options_gui){
    // Export Options:
    $options_gui.children[$options_gui.gui_ptr]  = text_message_new($options_gui, -290, -80, "Export AS:", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.export_as_label = $options_gui.children[$options_gui.gui_ptr].str[0].text
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = check_box_new($options_gui, -280, -90, "Patch Cards", CHECKBOX_ON, COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.export_as_cards_cb = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = check_box_new($options_gui, -120, -90, "Compressed Patch Banks", CHECKBOX_ON, COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.export_as_banks_cb = $options_gui.children[$options_gui.gui_ptr]	
	$options_gui.gui_ptr + 1
}

fn build_optimization_radio_button($options_gui){
    // Storage Optimization
    $options_gui.children[$options_gui.gui_ptr] = text_message_new($options_gui, -290, -30, "Optimization:", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.otmz_label = $options_gui.children[$options_gui.gui_ptr].str[0].text
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = check_box_new($options_gui, -280, -40, "None", CHECKBOX_OFF, COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.otmz_none_cb = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = check_box_new($options_gui, -120, -40, "Normal", CHECKBOX_ON, COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.otmz_normal_cb = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

    $options_gui.children[$options_gui.gui_ptr] = check_box_new($options_gui, 40, -40, "Aggressive (Unstable)", CHECKBOX_OFF, COLOR_DANGER)
	$options_gui.gui_comp_lkup.otmz_aggressive_cb = $options_gui.children[$options_gui.gui_ptr]
	$options_gui.gui_ptr + 1

	$options_gui.gui_comp_lkup.otmz_aggressive_cb.str[0].color = COLOR_DANGER
}

fn build_message_box($options_gui){
    // Message Box, window frame with 
    $options_gui.children[$options_gui.gui_ptr] = gui_window_frame_new($options_gui, 0, 125, 720, 200, "Messages", COLOR_DEFAULT)
	$options_gui.gui_comp_lkup.msg_box = $options_gui.children[$options_gui.gui_ptr]

	GUI_add_text($options_gui.children[$options_gui.gui_ptr].children[0])
	$options_gui.gui_comp_lkup.msg_box_text = $options_gui.children[$options_gui.gui_ptr].children[0].str[0]

	$options_gui.gui_comp_lkup.msg_box_text.text = APP_MSGS.INFO
	$options_gui.gui_comp_lkup.msg_box_text.x - 335
	$options_gui.gui_comp_lkup.msg_box_text.y - 45
	$options_gui.gui_comp_lkup.msg_box_text.flags = 0b00000101 
	$options_gui.gui_comp_lkup.msg_box_text.color = COLOR_LIGHT

	$options_gui.gui_ptr + 1
}

// *********************************************************************************************
// Process the behavior of the GUI
// 
// Input   : 
//   - $options_gui: GUI container
// behavior: 
// 	 - callback on button clicks
// 	 - radio button actions
// 	 - calling native text editor and file dialogs
//
// ********************************************************************************************* 
fn handle_options_events($options_gui){

	$gui_comp = $options_gui.gui_comp_lkup

    while get_event()
    {
        if EVT[ EVT_TYPE ] == EVT_QUIT { halt }
        $options_gui.check( $options_gui ) //call check function
        $options_gui.evt( $options_gui ) //call interaction handler

        // trigger text editor for the test editing buttons
		if $gui_comp.input_path_text_box.has_clicked == 1 {
			click_and_edit_text ($gui_comp.input_path_text_box, $gui_comp.input_path_label)
		}

		if $gui_comp.file_name_text_box.has_clicked == 1 {
			click_and_edit_text ($gui_comp.file_name_text_box, $gui_comp.file_name_label)
		}

		if $gui_comp.output_path_text_box.has_clicked == 1 {
			click_and_edit_text ($gui_comp.output_path_text_box, $gui_comp.output_path_label)
		}
        
		// trigger file dialog on button press
		if $gui_comp.input_path_load_btn.has_clicked == 1 {
			click_and_pick_file ($gui_comp.input_path_load_btn, $gui_comp.input_path_text_box, $gui_comp.file_name_label)
		}

		if $gui_comp.output_path_load_btn.has_clicked == 1 {
			click_and_pick_file ($gui_comp.output_path_load_btn, $gui_comp.output_path_text_box, $gui_comp.output_path_label)
		}

		// radio button actions
		if $gui_comp.otmz_none_cb.has_checked == 1 && $options_gui.export_as_radio_btn_state != 0{
			check_box_forced_off($gui_comp.otmz_normal_cb)
			check_box_forced_off($gui_comp.otmz_aggressive_cb)
			$options_gui.export_as_radio_btn_state = 0
		}

		if $gui_comp.otmz_normal_cb.has_checked == 1 && $options_gui.export_as_radio_btn_state != 1{
			check_box_forced_off($gui_comp.otmz_none_cb)
			check_box_forced_off($gui_comp.otmz_aggressive_cb)
			$options_gui.export_as_radio_btn_state = 1
		}

		if $gui_comp.otmz_aggressive_cb.has_checked == 1 && $options_gui.export_as_radio_btn_state != 2{
			check_box_forced_off($gui_comp.otmz_none_cb)
			check_box_forced_off($gui_comp.otmz_normal_cb)
			$options_gui.export_as_radio_btn_state = 2
		}

		generate_message_guide($options_gui)
    }
}

// *********************************************************************************************
// Display descriptions of the controllers when the mouse reach to cetain locations
// 
// Input: 
//   - $text_box_button: button object, 
//	 - $label          : label object
// Behavior: 
//   - call the native text editing dialog
//   - generate the trimmed version of file path for display
//   - store the actual data in str_full instead of str, at the same child level 
//
// ********************************************************************************************* 
fn generate_message_guide($options_gui){

	$gui_comp = $options_gui.gui_comp_lkup
	$current_x = EVT[ EVT_X ]
	$current_y = EVT[ EVT_Y ]

	// Ah yes, code repeatition. I need to find a better way to make this code cleaner to read using pixilang.
	while 1 { 
		if effective_component_region_check ($current_x, $current_y, $gui_comp.input_path_text_box) {
			$gui_comp.msg_box_text.text = APP_MSGS.IN_PATH_TXT_BOX
			break;

		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.input_path_load_btn) {
			$gui_comp.msg_box_text.text = APP_MSGS.IN_PATH_LOAD_BTN
			break;

		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.input_path_load_btn) {
			$gui_comp.msg_box_text.text = APP_MSGS.IN_PATH_LOAD_BTN
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.file_name_text_box) {
			$gui_comp.msg_box_text.text = APP_MSGS.FILE_NAME_TXT_BOX
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.output_path_text_box) {
			$gui_comp.msg_box_text.text = APP_MSGS.OUT_PATH_NOT_SUPPORT
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.output_path_load_btn) {
			$gui_comp.msg_box_text.text = APP_MSGS.OUT_PATH_LOAD_BTN
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.export_as_cards_cb) {
			$gui_comp.msg_box_text.text = APP_MSGS.EXPORT_PATCH_CARDS
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.export_as_banks_cb) {
			$gui_comp.msg_box_text.text = APP_MSGS.EXPORT_PATCH_BANKS
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.otmz_none_cb) {
			$gui_comp.msg_box_text.text = APP_MSGS.OTMZ_LV_NONE
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.otmz_normal_cb) {
			$gui_comp.msg_box_text.text = APP_MSGS.OTMZ_LV_NORMAL
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.otmz_aggressive_cb) {
			$gui_comp.msg_box_text.text = APP_MSGS.OTMZ_LV_AGGRESSIVE
			break;
			
		} /*else*/ if effective_component_region_check ($current_x, $current_y, $gui_comp.export_btn) {
			$gui_comp.msg_box_text.text = APP_MSGS.EXPORT_BTN
			break;
			
		} else {
			$gui_comp.msg_box_text.text = APP_MSGS.INFO
			break;
		}
	} 
}

fn effective_component_region_check ($current_x, $current_y, $components){
	
	$x_start = $components.x
	$x_end   = $x_start + $components.w

	$y_start = $components.y
	$y_end   = $y_start + $components.h

	ret (effective_direct_region_check($current_x, $current_y, $x_start, $x_end, $y_start, $y_end))
}

fn effective_direct_region_check ($current_x, $current_y, $x_start, $x_end, $y_start, $y_end){
	if $current_x >= $x_start && $current_x <= $x_end 
	  && $current_y >= $y_start && $current_y <= $y_end {
		ret (1)
	}else {
		ret (0)
	}
}

// *********************************************************************************************
// edit text from text input
// 
// Input: 
//   - $text_box_button: button object, 
//	 - $label          : label object
// Behavior: 
//   - call the native text editing dialog
//   - generate the trimmed version of file path for display
//   - store the actual data in str_full instead of str, at the same child level 
//
// ********************************************************************************************* 
fn click_and_edit_text ($text_box_button, $label){

    $modified_text = textinput_dialog ($text_box_button.children[0].str_full, $label)
	$text_box_button.children[0].str[0].text = trim_long_string ($modified_text)
	$text_box_button.children[0].str_full = $modified_text

	click_event_has_sent($text_box_button)
}

// *********************************************************************************************
// Load path from file dialog
// 
// Input: 
//	 - $load_button	   : button object, 
//   - $text_box_button: button object, 
//	 - $label          : label object
// Behavior: 
//   - call the native file dialog
//   - generate the trimmed version of file path for display
//   - store the actual data in str_full instead of str, at the same child level 
//
// ********************************************************************************************* 
fn click_and_pick_file ($load_button, $text_box_button, $label) {

	$file_path = file_dialog($label, "", $label, "")
	$text_box_button.children[0].str[0].text = trim_long_string ($file_path)
	$text_box_button.children[0].str_full = $file_path

	click_event_has_sent($load_button)
}

// *********************************************************************************************
// Trim strings that is way too long for display
// 
// Input: 
//   - $modified_text: String Container, 
// Behavior: 
//   - if the string contains more than 60 characters, only the last 60 characters will be kept,
//     and the head of string will be replaced by "..."
// ********************************************************************************************* 
fn trim_long_string ($modified_text){

	if strlen($modified_text) >= 60 {
		$temp_str = string_new()
		strcat($temp_str, "...")
		strcat($temp_str, 0, $modified_text, strlen($modified_text) - 60)

		ret($temp_str)
	} else {
		ret($modified_text)
	}
}
